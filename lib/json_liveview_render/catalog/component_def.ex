defmodule JsonLiveviewRender.Catalog.PropDef do
  @moduledoc "Prop definition metadata used by catalog components."

  @enforce_keys [:name, :type]
  defstruct [
    :name,
    :type,
    :doc,
    :validator,
    :binding_type,
    required: false,
    default: nil,
    values: nil
  ]

  @type prop_type ::
          :string
          | :integer
          | :float
          | :boolean
          | :map
          | :custom
          | :enum
          | {:list, prop_type()}

  @type t :: %__MODULE__{
          name: atom(),
          type: prop_type(),
          required: boolean(),
          default: term(),
          doc: String.t() | nil,
          values: [term()] | nil,
          validator: (term() -> boolean()) | nil,
          binding_type: prop_type() | nil
        }
end

defmodule JsonLiveviewRender.Catalog.ComponentDef do
  @moduledoc "Component definition generated by `JsonLiveviewRender.Catalog` DSL."

  alias JsonLiveviewRender.Catalog.PropDef

  @enforce_keys [:name]
  defstruct [
    :name,
    description: nil,
    props: %{},
    slots: [],
    permission: nil
  ]

  @type t :: %__MODULE__{
          name: atom(),
          description: String.t() | nil,
          props: %{optional(atom()) => PropDef.t()},
          slots: [atom()],
          permission: atom() | nil
        }

  @spec new(atom()) :: t()
  def new(name) when is_atom(name), do: %__MODULE__{name: name}

  @spec put_description(t(), String.t()) :: t()
  def put_description(component, description) when is_binary(description) do
    %{component | description: description}
  end

  @spec put_prop(t(), PropDef.t()) :: t()
  def put_prop(component, %PropDef{name: prop_name} = prop) do
    %{component | props: Map.put(component.props, prop_name, prop)}
  end

  @spec put_slot(t(), atom()) :: t()
  def put_slot(component, slot_name) when is_atom(slot_name) do
    %{component | slots: Enum.uniq(component.slots ++ [slot_name])}
  end

  @spec put_permission(t(), atom()) :: t()
  def put_permission(component, role) when is_atom(role) do
    %{component | permission: role}
  end

  @spec prop_names(t()) :: [atom()]
  def prop_names(component), do: component.props |> Map.keys() |> Enum.sort()
end
